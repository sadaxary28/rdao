plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "org.checkerframework" version "0.6.46" //https://github.com/kelloggm/checkerframework-gradle-plugin
}
if (project.hasProperty('ossrhUsername')) {
    apply from: 'publish.gradle'
}

group 'com.infomaximum'
version '1.14.15'

ext.moduleName = 'com.infomaximum.rdao'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
    }
    test {
        java.srcDir 'src/test/java'
        resources.srcDir 'src/test/resources'
    }
    test_performance {
        java.srcDir 'src/test_performance/java'
        resources.srcDir 'src/test_performance/resources'
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

configurations {
    test_performanceImplementation.extendsFrom(testImplementation)
    test_performanceRuntimeOnly.extendsFrom(testRuntimeOnly)
}

repositories {
    mavenCentral()
}

dependencies {

    implementation 'org.rocksdb:rocksdbjni:6.8.1'

    implementation('com.google.guava:guava:33.3.1-jre') {
        exclude group: 'com.google.guava', module: 'failureaccess'
        exclude group: 'com.google.guava', module: 'listenablefuture'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    implementation 'com.infomaximum:json-smart:2.5.1'

    implementation 'org.slf4j:slf4j-api:2.0.16'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testImplementation 'org.assertj:assertj-core:3.26.3'

    testImplementation 'ch.qos.logback:logback-core:1.5.12'
    testImplementation 'ch.qos.logback:logback-classic:1.5.12'

    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation 'org.apache.commons:commons-math:2.2'
}

task test_performance(type: Test, description: 'Performance test', group: 'Verification') {}